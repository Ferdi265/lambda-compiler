use std::*;
use io::*;
use super::util::*;
use super::tape::*;

pub EXECUTE = 0;
pub SKIP = 1;
pub RSKIP = 2;

pub impure run = y interpret -> state -> nestcount -> code -> data -> (
    set (code get_val) op ->
    set (data get_val) val ->
    switch state
        case EXECUTE (_ ->
            set (code move_right) code ->
            switch op
                case !0 (_ -> ident)
                case !'>' (_ ->
                    set (data move_right) data ->
                    interpret state nestcount code data
                )
                case !'<' (_ ->
                    set (data move_left) data ->
                    interpret state nestcount code data
                )
                case !'+' (_ ->
                    set (data set_val (rem (add val !1) !256)) data ->
                    interpret state nestcount code data
                )
                case !'-' (_ ->
                    set (data set_val (rem (add val !255) !256)) data ->
                    interpret state nestcount code data
                )
                case !'.' (_ ->
                    do (putc val)
                    interpret state nestcount code data
                )
                case !',' (_ ->
                    set (data set_val (io::getc ident)) data ->
                    interpret state nestcount code data
                )
                case !'[' (_ ->
                    if (iszero val)
                        then (_ ->
                            set SKIP state ->
                            set !0 nestcount ->
                            interpret state nestcount code data
                        )
                        else (_ ->
                            interpret state nestcount code data
                        )
                    fi
                )
                case !']' (_ ->
                    if (iszero val)
                        then (_ ->
                            interpret state nestcount code data
                        )
                        else (_ ->
                            set (code move_left) code ->
                            set (code move_left) code ->
                            set RSKIP state ->
                            set !0 nestcount ->
                            interpret state nestcount code data
                        )
                    fi
                )
                default (_ ->
                    interpret state nestcount code data
                )
            esac
        )
        case SKIP (_ ->
            set (code move_right) code ->
            switch op
                case !0 (_ -> ident)
                case !'[' (_ ->
                    set (succ nestcount) nestcount ->
                    interpret state nestcount code data
                )
                case !']' (_ ->
                    if (iszero nestcount)
                        then (_ ->
                            set EXECUTE state ->
                            interpret state nestcount code data
                        )
                        else (_ ->
                            set (pred nestcount) nestcount ->
                            interpret state nestcount code data
                        )
                    fi
                )
                default (_ ->
                    interpret state nestcount code data
                )
            esac
        )
        case RSKIP (_ ->
            set (code move_left) code ->
            switch op
                case !0 (_ -> ident)
                case !'[' (_ ->
                    if (iszero nestcount)
                        then (_ ->
                            set (code move_right) code ->
                            set EXECUTE state ->
                            interpret state nestcount code data
                        )
                        else (_ ->
                            set (pred nestcount) nestcount ->
                            interpret state nestcount code data
                        )
                    fi
                )
                case !']' (_ ->
                    set (succ nestcount) nestcount ->
                    interpret state nestcount code data
                )
                default (_ ->
                    interpret state nestcount code data
                )
            esac
        )
        default (_ -> ident)
    esac
);
